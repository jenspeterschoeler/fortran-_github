/TITLE, ANSYS
! Created by: JP and DRE

/clear,nostart  
/PREP7

! -------------------------------!
! Intialise parameters		 !
! -------------------------------!

eSIZE = 0.05

temp1 = 100
temp2 = 0

alpha1 = 10e-6
alpha2 = 100e-6


! -------------------------------!
! Element type			 !
! -------------------------------!
ET,	1,	PLANE222
! Setting thermal structural analysis in keyopt
KEYOPT,	1,	1,	11


! -------------------------------!
! Defining material properties	 !
! -------------------------------!

! MP from CES
MP,	EX,  	1,	100e3
MP,	PRXY,	1,	0.3
MP, 	KXX,	1,	1
MP,	CTEX, 	1, 	alpha1
R, 	1,	1  

MP,	EX,  	2,	100e3
MP,	PRXY,	2,	0.3
MP, 	KXX,	2,	1
MP,	CTEX, 	2, 	alpha2
R, 	1,	2  

MP,	EX,  	3,	100e3
MP,	PRXY,	3,	0.3
MP, 	KXX,	3,	0
MP,	CTEX, 	3, 	0
R, 	1,	3  


! -------------------------------!
! Geometry			 !
! -------------------------------!


w_connect = 0.3
h_connect = 4

h = 1
w = 10

! bottom uni metal beam

K,1,0,0,0
K,2,0.3,0,0
K,3,0,1,0
K,4,0.3,1,0
K,5,10,0,0
K,6,10,1,0

K,7,0.3,5,0
K,8,10,5,0
K,9,0.3,5.5,0
K,10,10,5.5,0
K,11,0,6,0
K,12,0.3,6,0
K,13,10,6,0

K,14,0,7,0
K,15,0.3,7,0

K,16,0,7,0
K,17,0.3,7,0
K,18,0.3,8,0
K,19,0,8,0

K,20,0,5,0
K,21,0,5.5,0


A,2,5,6,4
A,7,8,10,9

A,9,10,13,12

A,1,2,7,20
A,7,9,21,20
A,9,12,11,21

A,11,12,15,14
A,16,17,18,19


! -------------------------------!
! BC's	Before Mesh		 !
! -------------------------------!

LSEL,S,,,2
LSEL,A,,,6
LSEL,A,,,9
LSEL,A,,,25

! Structural BC on Line
DL,ALL, ,UX,0 
DL,ALL, ,UY,0 
ALLSEL,all

! -------------------------------!
! Mesh				 !
! -------------------------------!

ASEL,S,,,4
ASEL,A,,,5
ASEL,A,,,6
ASEL,A,,,7
ASEL,A,,,8
AATT,3,1
ALLSEL,all

ASEL,S,,,1
ASEL,A,,,2
AATT,1,1
ALLSEL,ALL

ASEL,S,,,3
AATT,2,1
ALLSEL,ALL

DK,8,TEMP,temp1
DK,6,TEMP,temp2

ALLSEL,ALL
! Meshing
AESIZE,all,eSIZE 
AMESH,ALL


! -------------------------------!
! Write CDB file		 !
! -------------------------------!
CDWRITE,DB,'relay_eSIZE005','cdb',,'','' 

! -------------------------------!
! Solve				 !
! -------------------------------!

FINISH  
/SOL
ANTYPE,0
SOLVE
FINISH


! -------------------------------!
! Post				 ! 
! -------------------------------!

/POST1
PRNSOL,TEMP 
PLNSOL, TEMP,, 0